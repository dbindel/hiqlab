#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ(2.57)
AC_INIT([HiQlab], [0.4], [dbindel@cs.berkeley.edu])
AC_CONFIG_AUX_DIR([config])
AC_CONFIG_SRCDIR([init.m])
AC_CONFIG_HEADER([config.h])
# AC_CONFIG_HEADER([src/trilinos/corelib/hiqlab_trilinos_config.h])

# Checks for programs.
AC_PROG_CC
AC_PROG_CXX
AC_PROG_F77
AC_PROG_MAKE_SET
AC_PROG_INSTALL
AC_PROG_RANLIB
AC_PROG_AWK
AC_PROG_PERL_VERSION(5.0)
AC_PROG_MEX
AC_CHECK_PROG(CYGPATH, cygpath, cygpath -w, echo)

AC_PATH_PROG(AR, ar)
ARFLAGS="rc"
AC_SUBST(AR)
AC_SUBST(ARFLAGS)

# Checks for libraries.
AC_F77_LIBRARY_LDFLAGS
AC_CHECK_LIB([m], [cos])
ACX_BLAS
ACX_LAPACK

#AC_WITH_BLACS
#AC_WITH_SCALAPACK
#AC_WITH_F90_HIQLAB
#AC_WITH_F90LIBS_HIQLAB
#AC_WITH_MUMPS
#AC_WITH_METIS
#AC_WITH_PARMETIS
#AC_WITH_TRILINOS
#AC_WITH_SUPERLU
#AC_WITH_SUPERLUDIST
#AC_WITH_PETSC
#AC_WITH_SLEPC
#AC_WITH_UMFPACK
#AC_WITH_ARPACK

ACX_LIB_READLINE

AC_ARG_WITH(fltk,
 AC_HELP_STRING([--with-fltk], [build with FLTK support (default no)]),
 [ac_with_fltk="$with_fltk"],
 [ac_with_fltk="no"])
HAVE_FLTK_TRUE="#"
if test x"$ac_with_fltk" != "xno"; then
  AC_PATH_PROG([FLTK_CONFIG], [fltk-config])
  if test x"$FLTK_CONFIG" != "x"; then
    AC_DEFINE(HAVE_FLTK, 1, [Define if FLTK is available])
    FLTK_CXXFLAGS="`$FLTK_CONFIG --cxxflags --use-gl`"
    FLTK_LDFLAGS="`$FLTK_CONFIG --ldflags --use-gl`"
    HAVE_FLTK_TRUE=""
  fi
fi
AC_SUBST(FLTK_CONFIG)
AC_SUBST(FLTK_CXXFLAGS)
AC_SUBST(FLTK_LDFLAGS)
AC_SUBST(HAVE_FLTK_TRUE)

AC_ARG_WITH(gd,
 AC_HELP_STRING([--with-gd], [build with GD support (default no)]),
 [ac_with_gd="$with_gd"],
 [ac_with_gd="no"])
HAVE_GD_TRUE="#"
if test x"$ac_with_gd" != "xno"; then
  AC_PATH_PROG([GD_CONFIG], [gdlib-config])
  if test x"$GD_CONFIG" != "x"; then
    AC_DEFINE(HAVE_GD, 1, [Define if GD is available])
    GD_CFLAGS="`$GD_CONFIG --cflags`"
    GD_LDFLAGS="`$GD_CONFIG --ldflags` -lgd `$GD_CONFIG --libs`"
    HAVE_GD_TRUE=""
  fi
fi
AC_SUBST(GD_CONFIG)
AC_SUBST(GD_CFLAGS)
AC_SUBST(GD_LDFLAGS)
AC_SUBST(HAVE_GD_TRUE)


# Checks for header files.
AC_FUNC_ALLOCA
AC_HEADER_STDC
AC_CHECK_HEADERS([limits.h locale.h malloc.h stddef.h stdlib.h string.h unistd.h])

# Checks for typedefs, structures, and compiler characteristics.
AC_C_CONST
AC_TYPE_SIZE_T
AC_STRUCT_TM
AC_HEADER_STDBOOL
AC_C_VOLATILE

AC_CHECK_SIZEOF(short)
AC_CHECK_SIZEOF(int)
AC_CHECK_SIZEOF(long)
AC_MSG_CHECKING(For 32-bit integer type)
if test "$ac_cv_sizeof_long" = 4; then
    HIQINT32=long
elif test "$ac_cv_sizeof_int" = 4; then
    HIQINT32=int
elif test "$ac_cv_sizeof_short" = 4; then
    HIQINT32=short
else
    AC_WARN([Can't find 32-bit type, using int])
    HIQINT32=int
fi
AC_SUBST(HIQINT32)
AC_MSG_RESULT(${HIQINT32})

# Checks for library functions.
AC_FUNC_ERROR_AT_LINE
AC_FUNC_FORK
AC_FUNC_MALLOC
AC_FUNC_MEMCMP
AC_FUNC_REALLOC
AC_TYPE_SIGNAL
AC_FUNC_STAT
AC_FUNC_STRCOLL
AC_FUNC_STRFTIME
AC_FUNC_VPRINTF
AC_CHECK_FUNCS([floor memchr memset pow setlocale sqrt strchr strcspn strdup strerror strpbrk strrchr strstr strtoul])

# Get host and date for banner
AC_CANONICAL_HOST
HIQ_HOST=$host
AC_SUBST(HIQ_HOST)
HIQ_DATE=`date`
AC_SUBST(HIQ_DATE)

AC_CONFIG_FILES([make.inc init.lua 
   src/corelib/hiqlab.h
   tools/lua/config tools/tolua++/config 
   tools/mwrap/make.inc tools/matexpr/make.inc
   tools/ARPACK/ARmake.inc tools/UFconfig/UFconfig.mk])

#   tools/metis-4.0/Makefile.in
#   src/trilinos/corelib/make.inc src/petsc/make.inc 
#   tools/config/parmetis-make.inc tools/config/superlu-make.inc 
#   tools/config/trilinos-conf tools/config/petsc-conf])

AC_OUTPUT
